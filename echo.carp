(load "SafeInt.carp")

(defn ignore-result [a] ())

;; TODO: Return a Maybe String
(defn escape-string-inner [s current-index]
      (if (>= current-index (String.length s))
	@""
	(let [current-char (String.char-at s current-index)]
	  (if (= current-char \\)
	    ;; TODO: check overflows
	    (let [next-char (String.char-at s (inc current-index))
		  replacement-char (cond
				     (= \a next-char) (Char.from-int 7)
				     (= \b next-char) (Char.from-int 8)
				     (= \t next-char) (Char.from-int 9)
				     (= \n next-char) (Char.from-int 10)
				     (= \v next-char) (Char.from-int 11)
				     (= \f next-char) (Char.from-int 12)
				     (= \r next-char) (Char.from-int 13)
				     ;; TODO: support \c \e \0NNN \xHH
				     next-char)]
	      ;; TODO: Can this be done with less mallocs?
	      (String.append
		&(String.from-chars &[replacement-char])
		&(escape-string-inner s (inc (inc current-index)))))
	    (String.append
	      &(String.from-chars &[current-char])
	      &(escape-string-inner s (inc current-index)))))))

(defn escape-string [s]
      (escape-string-inner s 0))

(defn echo-arguments [first-argument-index
		      last-argument-index
		      echo-newline
		      escape-arguments]
      (do
	(let [counter first-argument-index]
	  (while (< counter last-argument-index)
		 (do
		   (if escape-arguments
		     (IO.print &(escape-string (System.get-arg counter)))
		     (IO.print (System.get-arg counter)))
		   (IO.print " ")
		   (ignore-result (Int.safe-add counter 1 &counter)))))
	(if echo-newline
	  (IO.print "\n")
	  ())))

(defn main []
      (let [argc (System.get-args-len)
	    add-newline true
	    escape-characters false
	    first-non-flag-argument-index 0]
	(do

	  ;; Find flags
	  (let [argument-index 1]
	    (do
	      (while (< argument-index argc)
		     (do
		       (cond
			 (= (System.get-arg argument-index) "-e") (set! escape-characters true)
			 (= (System.get-arg argument-index) "-E") (set! escape-characters false)
			 (= (System.get-arg argument-index) "-n") (set! add-newline false)
			 (break))
		       (set! argument-index (inc argument-index))))
	      (set! first-non-flag-argument-index argument-index)))

	  (echo-arguments
	    first-non-flag-argument-index
	    argc
	    add-newline
	    escape-characters))))
