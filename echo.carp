(Project.config "title" "echo")

(defn escape-string [string-ref]
      (let [escaped-string (String.allocate (String.length string-ref) (Char.from-int 1)) ;; Can't put 0 because strlen is used in assertions
	    escaped-string-length 0
	    string-ref-index 0
	    string-ref-length (String.length string-ref)]
	(do
	  (while (< string-ref-index string-ref-length)
		 (let [current-char (String.char-at string-ref string-ref-index)
		       next-char-index (inc string-ref-index)]
		   (if (and (= current-char \\) (< next-char-index string-ref-length))
		     ;; Escape sequence
		     (let [next-char (String.char-at string-ref next-char-index)
				     replacement-char (cond
							(= \a next-char) (Char.from-int 7)
							(= \b next-char) (Char.from-int 8)
							(= \t next-char) (Char.from-int 9)
							(= \n next-char) (Char.from-int 10)
							(= \v next-char) (Char.from-int 11)
							(= \f next-char) (Char.from-int 12)
							(= \r next-char) (Char.from-int 13)
							;; TODO: support \c \e \0NNN \xHH
							next-char)]
		       (do
			 (String.string-set! &escaped-string escaped-string-length replacement-char)
			 (set! string-ref-index (+ string-ref-index 2))
			 (set! escaped-string-length (inc escaped-string-length))))

		     ;; Normal character
		     (do
		       (String.string-set! &escaped-string escaped-string-length current-char)
		       (set! string-ref-index (inc string-ref-index))
		       (set! escaped-string-length (inc escaped-string-length))))))

	  (String.slice &escaped-string 0 escaped-string-length))))

(defn echo-arguments [first-argument-index
		      last-argument-index
		      echo-newline
		      escape-arguments]
      (do
	(let [counter first-argument-index]
	  (while (< counter last-argument-index)
		 (do
		   (if escape-arguments
		     (IO.print &(escape-string (System.get-arg counter)))
		     (IO.print (System.get-arg counter)))
		   (set! counter (inc counter))
		   (when (>= counter last-argument-index)
		     (break))
		   (IO.print " "))))
	(when echo-newline
	  (IO.print "\n"))))

(defn main []
      (let [argc (System.get-args-len)
	    add-newline true
	    escape-characters false
	    first-non-flag-argument-index 0]
	(do

	  ;; Find flags
	  (let [argument-index 1]
	    (do
	      (while (< argument-index argc)
		     (do
		       (cond
			 ;; TODO: Support -en -ne etc
			 (= (System.get-arg argument-index) "-e") (set! escape-characters true)
			 (= (System.get-arg argument-index) "-E") (set! escape-characters false)
			 (= (System.get-arg argument-index) "-n") (set! add-newline false)
			 (break))
		       (set! argument-index (inc argument-index))))
	      (set! first-non-flag-argument-index argument-index)))

	  (echo-arguments
	    first-non-flag-argument-index
	    argc
	    add-newline
	    escape-characters))))
